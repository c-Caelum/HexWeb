plugins {
    id 'com.github.johnrengelman.shadow'
    id "me.modmuss50.mod-publish-plugin" version "0.8.4"
}

loom {
    forge {
        mixinConfig "hexweb.mixins.json"
    }
}

architectury {
    platformSetupLoomIde()
    forge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    maven {
        name = "C4's Maven"
        url = "https://maven.theillusivec4.top/"
    }
    maven {
        name = 'Kotlin for Forge'
        url = 'https://thedarkcolour.github.io/KotlinForForge/'
        content { includeGroup "thedarkcolour" }
    }
}

dependencies {
    forge "net.minecraftforge:forge:$rootProject.forge_version"

    // Architectury API. This is optional, and you can comment it out if you don't need it.
    modImplementation "dev.architectury:architectury-forge:$rootProject.architectury_api_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionForge')

    modImplementation("at.petra-k.hexcasting:hexcasting-forge-${minecraft_version}:${hexcasting_version}") { transitive = false }
    implementation "thedarkcolour:kotlinforforge:4.11.0"
    modCompileOnly "at.petra-k.paucal:paucal-forge-${minecraft_version}:${paucal_version}"
    modRuntimeOnly "at.petra-k.paucal:paucal-forge-${minecraft_version}:${paucal_version}"
    modCompileOnly "vazkii.patchouli:Patchouli:${minecraft_version}-${patchouli_version}-FORGE-SNAPSHOT:api"
    modRuntimeOnly "vazkii.patchouli:Patchouli:${minecraft_version}-${patchouli_version}-FORGE-SNAPSHOT"
    modRuntimeOnly "com.samsthenerd.inline:inline-forge:$minecraft_version-$inline_version"
    modCompileOnly ("top.theillusivec4.caelus:caelus-forge:${caelus_version}+1.20.1:api") { transitive = false }
    modRuntimeOnly ("top.theillusivec4.caelus:caelus-forge:${caelus_version}+1.20.1") { transitive = false }

    modImplementation("ram.talia.moreiotas:moreiotas-forge-$minecraft_version:$more_iotas_version") { transitive = false }
    modImplementation("ram.talia.hexal:hexal-forge-$minecraft_version:$hexal_version") { transitive = false }
    modCompileOnly "penguinencounter.mediatransport:mediatransport-forge:$mediatransport_version+$minecraft_version"
}

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/mods.toml') {
        expand version: project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
}

publishMods {
    file = remapJar.archiveFile
    additionalFiles.from(sourcesJar.archiveFile)
    changelog = getLatestChangelog()
    type = STABLE
    modLoaders.add("forge")

    version = project.version + "-forge"
    displayName = "HexWeb $project.version [Forge]"

    curseforge {
        accessToken = project.curseforge_api_token
        projectId = project.curseforge_id
        minecraftVersions.add(minecraft_version)

        requires("architectury-api", "kotlin-for-forge", "hexcasting", "moreiotas")
    }

    modrinth {
        accessToken = project.modrinth_api_token
        projectId = project.modrinth_id
        minecraftVersions.add(minecraft_version)

        requires("architectury-api")
        requires("kotlin-for-forge")
        requires("hex-casting")
        requires("moreiotas")

        optional("hexical")
    }
}
