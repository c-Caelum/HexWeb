plugins {
    id 'com.github.johnrengelman.shadow'
    id "me.modmuss50.mod-publish-plugin" version "0.8.4"
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    maven {
        name = 'Ladysnake Mods'
        url = 'https://maven.ladysnake.org/releases'
    }
    maven {
        name = 'GeckoLib'
        url = 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/'
        content {
            includeGroupByRegex("software\\.bernie.*")
            includeGroup("com.eliotlash.mclib")
        }
    }
    maven {
        name = "Fuzs Mod Resources"
        url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/"
    }
    maven { url = "https://maven.kosmx.dev/" }
    maven { url = "https://maven.terraformersmc.com/releases" }
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:$rootProject.fabric_loader_version"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:$rootProject.fabric_api_version"
    modImplementation("net.fabricmc:fabric-language-kotlin:1.10.20+kotlin.1.9.24")

    // Architectury API. This is optional, and you can comment it out if you don't need it.
    modImplementation "dev.architectury:architectury-fabric:$rootProject.architectury_api_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionFabric')

    modImplementation "vazkii.patchouli:Patchouli:${minecraft_version}-${patchouli_version}-FABRIC"
    modImplementation "at.petra-k.hexcasting:hexcasting-fabric-${minecraft_version}:${hexcasting_version}"
    modRuntimeOnly "at.petra-k.paucal:paucal-fabric-${minecraft_version}:${paucal_version}"
    modRuntimeOnly "com.samsthenerd.inline:inline-fabric:$minecraft_version-$inline_version"
    modRuntimeOnly "dev.onyxstudios.cardinal-components-api:cardinal-components-api:${cca_version}"
    modRuntimeOnly "io.github.tropheusj:serialization-hooks:0.4.99999"
    modImplementation("software.bernie.geckolib:geckolib-fabric-${minecraft_version}:${geckolib_version}")
    modImplementation("me.shedaniel.cloth:cloth-config-fabric:${cloth_config_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    modImplementation ("ram.talia.moreiotas:moreiotas-fabric-$minecraft_version:$more_iotas_version") { transitive = false }
    modImplementation ("ram.talia.hexal:hexal-fabric-$minecraft_version:$hexal_version") { transitive = false }
    modImplementation "maven.modrinth:hexical:$hexical_version"
    modCompileOnly "penguinencounter.mediatransport:mediatransport-fabric:$mediatransport_version+$minecraft_version"
    modRuntimeOnly "dev.kosmx.player-anim:player-animation-lib-fabric:1.0.2-rc1+1.20"

    modImplementation "fuzs.forgeconfigapiport:forgeconfigapiport-fabric:8.0.0"
}

processResources {
    inputs.property 'version', project.version

    filesMatching('fabric.mod.json') {
        expand version: project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
}

publishMods {
    file = remapJar.archiveFile
    additionalFiles.from(sourcesJar.archiveFile)
    changelog = getLatestChangelog()
    type = STABLE
    modLoaders.add("fabric")
    modLoaders.add("quilt")

    version = project.version + "-fabric"
    displayName = "HexWeb $project.version [Fabric]"

    curseforge {
        accessToken = project.curseforge_api_token
        projectId = project.curseforge_id
        minecraftVersions.add(minecraft_version)

        requires("fabric-api", "architectury-api", "kotlin-for-forge", "hexcasting", "moreiotas")
    }

    modrinth {
        accessToken = project.modrinth_api_token
        projectId = project.modrinth_id
        minecraftVersions.add(minecraft_version)

        requires("architectury-api")
        requires("kotlin-for-forge")
        requires("hex-casting")
        requires("moreiotas")

        optional("hexical")
    }
}
